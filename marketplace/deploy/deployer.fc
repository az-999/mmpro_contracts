;; Deployer for nft sales
#include "../../op-codes.fc";

int op::do_sale() asm "0x0fe0ede PUSHINT";

;; storage scheme
;; storage#_ owner_address:MsgAddress
;;           = Storage;


() recv_internal(int balance, int msg_value, cell in_msg_full, slice in_msg_body) impure { ;; –æ–±—Ä–∞–±–æ—Ç–∫–∞ —Å–æ–æ–±—â–µ–Ω–∏–π
    if (in_msg_body.slice_empty?()) { ;; –ø—Ä–æ–ø—É—Å–∫–∞–µ–º –ø—É—Å—Ç—ã–µ —Å–æ–æ–±—â–µ–Ω–∏—è
        return ();
    }
    slice cs = in_msg_full.begin_parse(); ;; –ø–∞—Ä—Å–∏–Ω–≥ –≤—Ö–æ–¥—è—â–µ–≥–æ —Å–æ–æ–±—â–µ–Ω–∏—è
    int flags = cs~load_uint(4);          ;; –ø–æ–ª—É—á–∞–µ–º —Ñ–ª–∞–≥–∏

    if (flags & 1) {  ;; –ø—Ä–æ–ø—É—Å–∫–∞–µ–º —Å–æ–æ–±—â–µ–Ω–∏—è —Å —Ñ–ª–∞–≥–æ–º bounced
        return ();
    }
    slice sender_address = cs~load_msg_addr(); ;; –ø–æ–ª—É—á–∞–µ–º –∞–¥—Ä–µ—Å –æ—Ç–ø—Ä–∞–≤–∏—Ç–µ–ª—è
    ;; cs.end_parse();

    int op = in_msg_body~load_uint(32); ;; –ø–æ–ª—É—á–∞–µ–º –æ–ø–∫–æ–¥

    if (op == op::ownership_assigned()) { ;; –ø—Ä–æ–≤–µ—Ä—è–º, —á—Ç–æ —ç—Ç–æ –æ–ø–∫–æ–¥ ownership_assigned (0x05138d91)
      int query_id = in_msg_body~load_uint(64); ;; —Å—á–∏—Ç—ã–≤–∞–µ–º quer_id
      var prev_owner = in_msg_body~load_msg_addr(); ;; —Å—á–∏—Ç—ã–≤–∞–µ–º –∞–¥—Ä–µ—Å –ø—Ä–µ–¥—ã–¥—É—â–µ–≥–æ –≤–ª–∞–¥–µ–ª—å—Ü–∞ (–∫—Ç–æ –æ—Ç–ø—Ä–∞–≤–∏–ª –Ω—Ñ—Ç)
      int sub_op = in_msg_body~load_uint(32); ;; —Å—á–∏—Ç—ã–≤–∞–µ–º –ø–æ–¥–æ–ø–µ—Ä–∞—Ü–∏—é
      throw_if(404, sub_op != op::do_sale()); ;; –ø—Ä–æ–≤–µ—Ä—è–µ–º, —á—Ç–æ –æ–ø–µ—Ä–∞—Ü–∏—è - –≤—ã—Å—Ç–∞–≤–ª–µ–Ω–∏–µ –Ω–∞ –ø—Ä–æ–¥–∞–∂—É (0x0fe0ede)
      (cell state_init, cell body) = (in_msg_body~load_ref(), in_msg_body~load_ref()); ;; –ø–æ–ª—É—á–∞–µ–º –¥–∞–Ω–Ω—ã–µ –¥–ª—è –¥–µ–ø–ª–æ—è —Å–µ–π–ª–∞
      in_msg_body.end_parse(); ;; –ø–∞—Ä—Å–∏–Ω–≥ –æ–∫–æ–Ω—á–µ–Ω
      int state_init_hash = cell_hash(state_init); ;; –ø–æ–ª—É—á–∞–µ–º —Ö—ç—à –¥–∞–Ω–Ω—ã—Ö –¥–ª—è –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏–∏ —Å–µ–π–ª–∞
      slice dest_address = begin_cell().store_int(0, 8).store_uint(state_init_hash, 256).end_cell().begin_parse(); ;; –∏ –≤—ã—á–∏—Å–ª—è–µ–º –∞–¥—Ä–µ—Å

      var msg = begin_cell() ;; —Å–æ–±–∏—Ä–∞–µ–º —Å–æ–æ–±—â–µ–Ω–∏—è –¥–ª—è –¥–µ–ø–ª–æ—è
        .store_uint(0x18, 6) ;; —è —Ö–∑ —á–µ —ç—Ç–æ
        .store_uint(4, 3).store_slice(dest_address) ;; –ª–æ–∂–∏–º –∞–¥—Ä–µ—Å, –Ω–∞ –∫–æ—Ç–æ—Ä–æ–º –±—É–¥–µ—Ç –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∏—Ä–æ–≤–∞–Ω —Å–µ–π–ª
        .store_grams(20000000) ;; 0.02 TON - –¥–ª—è –∫–æ–º–∏—Å—Å–∏–π
        .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1) ;; —Ö–∑
        .store_ref(state_init) ;; –¥–∞–Ω–Ω—ã–µ –¥–ª—è –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏–∏
        .store_ref(body) ;; —Ç–æ–∂–µ
        .end_cell();
      send_raw_message(msg, 1); ;; –¥–µ–ø–ª–æ–π —Å–µ–π–ª–∞

      var transfer_msg = begin_cell() ;; –∑–∞—Ç–µ–º —Å–æ–±–∏—Ä–∞–µ–º —Å–æ–æ–±—â–µ–Ω–∏–µ –¥–ª—è –ø–µ—Ä–µ–≤–æ–¥–∞ –Ω—Ñ—Ç —Å–µ–π–ª—É
              .store_uint(0x18, 6)
              .store_slice(sender_address)
              .store_grams(0)
              .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
              .store_uint(op::transfer(), 32)
              .store_uint(query_id, 64)
              .store_uint(4, 3).store_slice(dest_address) ;; new owner
              .store_slice(prev_owner) ;; response_to
              .store_int(0, 1) ;; no payload
              .store_grams(0) ;; forward_amount
              .store_int(0, 1) ;; no body
              .end_cell();
      var reserve = balance - msg_value; ;; —Ä–µ–∑–µ—Ä–≤–∏—Ä—É–µ–º —Ç–æ–Ω—ã –¥–ª—è –∫–æ–º–∏—Å—Å–∏–π
      if (reserve < 1000000) {
        reserve = 1000000; ;; 0.001 TON
      } else {
        reserve += 1000; ;; 0.000001 TON for storage usage
      }
      raw_reserve(reserve, 0); ;; reserve some bebras  üêà
      send_raw_message(transfer_msg, 128); ;; –æ—Ç–ø—Ä–∞–≤–ª—è–µ–º –Ω—Ñ—Ç —Å–µ–π–ª—É

      return ();
    }
;;
    if (op == 1) { ;; accept for deploy
      return ();
    }

    if (op == 555) { ;; –ø—Ä–æ–∫—Å–∏—Ä–æ–≤–∞–Ω–∏–µ —Å–æ–æ–±—â–µ–Ω–∏–π –∫ —Å–µ–π–ª–∞–º —ç—Ç–æ–≥–æ –º–∞—Ä–∫–µ—Ç–ø–ª–µ–π—Å–∞
      var ds = get_data().begin_parse(); ;; –ø–æ–ª—É—á–µ–º –¥–∞–Ω–Ω—ã–µ
      var owner_address = ds~load_msg_addr(); ;; –≤—ã—Ç–∞—Å–∫–∏–≤–∞–µ–º –∞–¥—Ä–µ—Å –æ–≤–Ω–µ—Ä–∞
      throw_unless(403, equal_slices(owner_address, sender_address)); ;; –ø—Ä–æ–≤–µ—Ä—è–µ–º, —á—Ç–æ —Å–æ–æ–±—â–µ–Ω–∏–µ –ø—Ä–∏—à–ª–æ –æ—Ç –Ω–µ–≥–æ
      ;; way to fix unexpected troubles with auction contract
      ;; for example if some one transfer nft to this contract
      var msg = in_msg_body~load_ref().begin_parse(); ;; –≤—ã—Ç–∞—Å–∫–∏–≤–∞–µ–º —Å–∞–º–æ —Å–æ–æ–±—â–µ–Ω–∏–µ
      var mode = msg~load_uint(8); ;; —Ä–µ–∂–∏–º –æ—Ç–ø—Ä–∞–≤–∫–∏
      send_raw_message(msg~load_ref(), mode); ;; –∞–¥—Ä–µ—Å —Å–µ–π–ª–∞ –≤—ã—Ç–∞—Å–∫–∏–≤–µ—Ç—Å—è –∏ –Ω–∞ –Ω–µ–≥–æ –ø–µ—Ä–µ–ø—Ä–∞–≤–ª—è–µ—Ç—Å—è —Å–æ–æ–±—â–µ–Ω–∏–µ
      return ();
    }

    throw(0xffff);
}

